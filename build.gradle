plugins {
    id 'java'
    id 'xyz.wagyourtail.jvmdowngrader' version '1.2.2'
    id 'xyz.wagyourtail.unimined' version '1.3.13'
    id 'com.github.johnrengelman.shadow' version "8.1.1"
}

group = project.maven_group
version = project.maven_version
description = project.maven_description

base {
    archivesName = project.maven_name
}

compileJava.options.encoding = "UTF-8"
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

idea {
    module {
        downloadJavadoc = false
        downloadSources = true
        inheritOutputDirs = true
    }
}

unimined.minecraft {
    version "1.8.9"

    minecraftForge {
        loader "11.15.1.2318-1.8.9"
        mixinConfig("mixins.viaforge.json")
    }

    mappings {
        searge()
        mcp("stable", "22-1.8.9")
    }

    remap(tasks.shadowJar) {
        prodNamespace("searge")
        mixinRemap {
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven {
        name = "Sponge"
        url 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = "ViaVersion"
        url = "https://repo.viaversion.com"
        metadataSources {
            artifact()
        }
    }
}

jvmdg.downgradeTo = JavaVersion.VERSION_1_8

configurations {
    library
    implementation.extendsFrom library
}

dependencies {
    library ("org.spongepowered:mixin:${project.mixin_version}") {
        exclude module: "launchwrapper"
    }
    library "com.viaversion:viaversion-common:5.3.1-SNAPSHOT"
    library "com.viaversion:viabackwards-common:5.3.1-SNAPSHOT"
    library "com.viaversion:viarewind-common:4.0.7-SNAPSHOT"
    library "com.viaversion:viaaprilfools-common:4.0.2-SNAPSHOT"
    library ("com.viaversion:vialoader:4.0.3-SNAPSHOT") {
        exclude group: "com.google.guava"
        exclude group: "org.slf4j"
    }
    library ("net.raphimc:ViaLegacy:3.0.10-SNAPSHOT") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    library ("org.slf4j:slf4j-api:${project.slf4j_version}")
}

processResources {
    filesMatching("mcmod.info") {
        expand "version": project.version
        expand "description": project.description
    }
}

jar {
    manifest {
        attributes([
            "FMLCorePlugin": "de.florianmichael.viaforge.mixin.MixinLoader",
            "FMLCorePluginContainsFMLMod": "true",
            "ForceLoadAsMod": "true",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "MixinConfigs": "mixins.viaforge-mc189.json",
            "Manifest-Version": 1.0
        ])
    }
    enabled = false
}

shadowJar {
    destinationDirectory = temporaryDir
    configurations = [project.configurations.library]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    exclude("META-INF/maven/**")
    exclude("META-INF/versions/**")
}

downgradeJar {
    inputFile = shadowJar.archiveFile
    destinationDirectory = temporaryDir
}

shadeDowngradedApi {
    archiveFileName = jar.archiveFileName
}

jar.dependsOn("shadeDowngradedApi")

tasks.build {
    dependsOn(tasks.shadeDowngradedApi)
}
