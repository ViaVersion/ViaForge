buildscript {

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()

        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://plugins.gradle.org/m2' }
        maven { url 'https://repo.spongepowered.org/maven' }
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '2.1-SNAPSHOT'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.6-SNAPSHOT'
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '4.0.4'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "java"

version = "${minecraft_version}-${mod_version}"
group = "${mod_base_package}.${mod_id}"
archivesBaseName = mod_id

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "${minecraft_version}-${forge_version}-${minecraft_version}"
    runDir = "run"
    mappings = "${mappings_channel}_${mappings_version}"
    makeObfSourceJar = true
}

sourceSets {
    main.java.srcDirs += 'src/main/java'
    main.java.srcDirs += '../shared/main/java'
    main.resources.srcDirs += '../shared/main/resources'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()

    maven {
        url 'https://jitpack.io'
    }
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven/'
    }

    maven {
        url = "https://repo.viaversion.com"
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    compile ("org.spongepowered:mixin:0.7.10-SNAPSHOT") {
        exclude module: "launchwrapper"
    }
    include "com.viaversion:viaversion:${viaversion_version}"
    include "com.viaversion:viabackwards:${viabackwards_version}"
    include "com.viaversion:viarewind-fabric:${viarewind_version}"
    include "org.yaml:snakeyaml:1.29"
    include "org.spongepowered:mixin:0.7.10-SNAPSHOT"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
}

jar {
    manifest.attributes(
            "MixinConfigs": "mixins.viaforge.json",
            "tweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": 0,
            "FMLCorePluginContainsFMLMod": "de.enzaxd.viaforge.injection.MixinLoader"
    )
}

apply plugin: "idea"

idea {
    module {
        inheritOutputDirs = true
    }
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.include]
    exclude "LICENSE.md"
    exclude "dummyThing"
    exclude "**/module-info.class"
    exclude "*.so"
    exclude "*.dylib"
    exclude "*.dll"
    exclude "*.jnilib"
    exclude "ibxm/**"
    exclude "com/jcraft/**"
    exclude "org/lwjgl/**"
    exclude "net/java/**"

    exclude "META-INF/proguard/**"
    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"
    exclude "META-INF/com.android.tools/**"

    exclude "fabric.mod.json"
    exclude "bungee.yml"
    exclude "plugin.yml"
    exclude "velocity-plugin.json"
    exclude "LICENSE.txt"

    exclude "mcmod.info"

    exclude "org/apache/**"
    exclude "com/google/**"
}

reobf {
    shadowJar {}
}

jar.dependsOn("shadowJar")
